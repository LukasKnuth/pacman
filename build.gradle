plugins {
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

sourceSets {
    main {
        def resource_subfolder = 'org/ita23/pacman/res'
        java {
            // We don't follow the default single-app layout
            srcDirs = ['src']
        }
        resources {
            // The resources (graphics/sounds) are stored along with the code
            srcDirs = ["${java.srcDirs.first()}/${resource_subfolder}"]
        }
        // The resources are loaded at runtime with relative paths, so they must
        // be located next to the classes in the output as well.
        output.resourcesDir = "${output.classesDirs.first()}/${resource_subfolder}"
    }
}

application {
    mainClass = 'org.ita23.pacman.Main'
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClass
    }

    // The JAR will contain the resources (graphics/sounds) twice, once from the
    // "output.classesDirs" output and once from the "resources.srcDirs".
    // We drop all that aren't in the top-level "org" folder because they aren't used.
    include { it.path.startsWith("org") }
}
